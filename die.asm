; TODO INSERT CONFIG CODE HERE USING CONFIG BITS GENERATOR
#include "p16f690.inc"

; CONFIG
; __config 0xF8F5
 __CONFIG _FOSC_INTRCCLK & _WDTE_OFF & _PWRTE_OFF & _MCLRE_ON & _CP_OFF & _CPD_OFF & _BOREN_OFF & _IESO_OFF & _FCMEN_ON

;CONSTANTS
DIE1 EQU b'01000000'
DIE2 EQU b'00010010'
DIE3 EQU b'01001100'
DIE4 EQU b'00101101'
DIE5 EQU b'01101101'
DIE6 EQU b'00111111'
 
;VARS:
LFSR	EQU 0X70
TW	EQU 0X71
TSTATUS EQU 0X72
    
RES_VECT  CODE    0x0000            ; processor reset vector
    GOTO    START                   ; go to beginning of program


    
;INTERRUPT HANDLER:
 ORG 0X0004
    MOVWF TW    ;SAVE W
    SWAPF STATUS,W  ;SAVE STATUS
    MOVWF TSTATUS
    
    MOVLW HIGH(LUT)
    MOVWF PCLATH
    MOVF LFSR,W
    CALL LUT	;FOR FASTER EXECUTION AND BECAUSE WE HAVE A LOT OF UNUSED SPACE ON THE FLASH I'VE OPTED TO USE A LOOKUP TABLE INSTEAD OF CALCULATING LFSR % 6 AND PUTTING THE APROPRIATE SIGNALS ON PORTC
    MOVWF PORTC

    SWAPF TSTATUS,W  
    MOVWF STATUS    ;RESTORE STATUS
    MOVF TW,W      ;RESTORE W
    BCF INTCON,RABIF
    RETFIE


    
    
MAIN_PROG CODE                      ; let linker place main program

START:
    BSF STATUS,RP0 ;BANK 1
    MOVLW 0X8F
    ANDWF OSCCON,F   ;SET LFINTOSC 31KHZ
    
    MOVLW 0X80
    MOVWF TRISC ;PORTC OUTPUT FOR LEDS (EXCEPT RC7, UNUSED SO INPUT)
    
    MOVLW 0XFF
    MOVWF TRISA ;PORTA INPUT BECAUSE UNUSED
    MOVWF TRISB ;PORTB INPUT FOR ADC
    
    BCF STATUS,RP0
    BSF STATUS,RP1 ;BANK 2
    
    CLRF ANSEL
    CLRF ANSELH
    BSF ANSELH,ANS11 ;SELECT ADC CHANNEL AN11
    
    BCF STATUS,RP1 ;BANK 0
    
    MOVLW 0X00	;-|
    MOVWF PORTC ;-|-WE TURN OFF THE LEDS UNTIL THE USER PRESSES THE BUTTON
   
    ;WE TAKE A MEASUREMENT WITH THE ADC ON A FLOATING PIN SO WE READ NOISE WHICH WE USE AS A RANDOM ENOUGH SEED FOR THE LFSR
    
    BSF ADCON0,ADON ;TURN ON ADC
    
    BSF ADCON0,CHS0 ;-|
    BSF ADCON0,CHS1 ;-|
    BCF ADCON0,CHS2 ;-|
    BSF ADCON0,CHS3 ;-|-SELECT AN11 AS INPUT FOR ADC
    
    BCF ADCON0,VCFG ;SELECT VDD AS REFFERENCE FOR ADC
    
TRYADC:
    BSF ADCON0,GO     ;-|
WAITADC:	      ;-|
    BTFSC ADCON0,GO   ;-|
    GOTO WAITADC      ;-|-START THE ADC READ AND WAITS FOR IT TO FINISH
    MOVF ADRESH,W
    BTFSC STATUS,Z    ;WE CANT USE 0 AS A SEED FOR THE LFSR SO WE READ AGAIN AND AGAIN UNTIL WE GET SOME NON ZERO VALUE
    GOTO TRYADC
    
    BCF ADCON0,ADON ;TURN OFF ADC FOR POWER SAVING

    MOVWF LFSR  ;STORE THE ADC RESULT IN THE LFSR
    
    BSF STATUS,RP0    ;BANK 1
    BCF OPTION_REG,7	  ;ENABLE WEAK PULL-UPS
    BCF STATUS,RP0    ;BANK 0
    
    BSF STATUS,RP1    ;BANK 2
    BSF WPUB,WPUB7	  ;ENABLE THE WEAK PULL-UP ON RB7
    BSF IOCB,IOCB7	  ;ENABLE INTRERRUPTS ON RB7 CHANGE
    BCF STATUS,RP1    ;BANK 0
   
    BSF INTCON,RABIE ;ENABLE INTRERRUPT ON PORT CHANGE
    BSF INTCON,GIE  ;ENABLE GLOBAL INTRERRUPTS
 

LOOP:
    MOVLW 0x00
    BTFSC LFSR,0 ;TAP BIT 0
    MOVLW 0x01
    BTFSC LFSR,2 ;TAP BIT 2
    XORLW 0x01
    BTFSC LFSR,3 ;TAP BIT 3
    XORLW 0x01
    BTFSC LFSR,4 ;TAP BIT 4
    XORLW 0x01
    
   
    BCF STATUS,C    ;-|
    BTFSS STATUS,Z  ;-| 
    BSF STATUS,C    ;-|-WE TEST THE ZERO FLAG TO SEE THE RESULT OF THE XOR OPERATION AND PUT IT INTO THE CARRY BIT SO THAT IT CAN BE ROTATED INTO THE LFSR REGISTER
    
    RRF LFSR,F  ;WE ROTATE THE REGISTER TO THE RIGHT BRINGING THE RESULT FROM CARRY INTO THE REGISTER
    
    GOTO LOOP                          ; loop forever
  


 ORG 0X0D00	    ;WE START AT THE TOP OF THE PAGE SO PCL DOESN'T OVERFLOW AND WE CAN USE ALL THE AVAILABLE 255 INSTRUCTIONS
LUT:		    ;THE LOOKUP TABLE BASICALLY DOES (LFSR % 6) + 1 BUT THE RESULT IS DIRECTLY IN THE FORMAT NEEDED FOR DISPLAYING THAT NUMBER ON THE LEDS
    ADDWF PCL,F     ;W IS ALWAYS NON ZERO BECAUSE THE LFSR IS ALWAYS NON ZERO SO NO RISK OF INFINITE LOOP
    
    RETLW DIE1
    RETLW DIE2
    RETLW DIE3
    RETLW DIE4
    RETLW DIE5
    RETLW DIE6
    RETLW DIE1
    RETLW DIE2
    RETLW DIE3
    RETLW DIE4
    RETLW DIE5
    RETLW DIE6
    RETLW DIE1
    RETLW DIE2
    RETLW DIE3
    RETLW DIE4
    RETLW DIE5
    RETLW DIE6
    RETLW DIE1
    RETLW DIE2
    RETLW DIE3
    RETLW DIE4
    RETLW DIE5
    RETLW DIE6
    RETLW DIE1
    RETLW DIE2
    RETLW DIE3
    RETLW DIE4
    RETLW DIE5
    RETLW DIE6
    RETLW DIE1
    RETLW DIE2
    RETLW DIE3
    RETLW DIE4
    RETLW DIE5
    RETLW DIE6
    RETLW DIE1
    RETLW DIE2
    RETLW DIE3
    RETLW DIE4
    RETLW DIE5
    RETLW DIE6
    RETLW DIE1
    RETLW DIE2
    RETLW DIE3
    RETLW DIE4
    RETLW DIE5
    RETLW DIE6
    RETLW DIE1
    RETLW DIE2
    RETLW DIE3
    RETLW DIE4
    RETLW DIE5
    RETLW DIE6
    RETLW DIE1
    RETLW DIE2
    RETLW DIE3
    RETLW DIE4
    RETLW DIE5
    RETLW DIE6
    RETLW DIE1
    RETLW DIE2
    RETLW DIE3
    RETLW DIE4
    RETLW DIE5
    RETLW DIE6
    RETLW DIE1
    RETLW DIE2
    RETLW DIE3
    RETLW DIE4
    RETLW DIE5
    RETLW DIE6
    RETLW DIE1
    RETLW DIE2
    RETLW DIE3
    RETLW DIE4
    RETLW DIE5
    RETLW DIE6
    RETLW DIE1
    RETLW DIE2
    RETLW DIE3
    RETLW DIE4
    RETLW DIE5
    RETLW DIE6
    RETLW DIE1
    RETLW DIE2
    RETLW DIE3
    RETLW DIE4
    RETLW DIE5
    RETLW DIE6
    RETLW DIE1
    RETLW DIE2
    RETLW DIE3
    RETLW DIE4
    RETLW DIE5
    RETLW DIE6
    RETLW DIE1
    RETLW DIE2
    RETLW DIE3
    RETLW DIE4
    RETLW DIE5
    RETLW DIE6
    RETLW DIE1
    RETLW DIE2
    RETLW DIE3
    RETLW DIE4
    RETLW DIE5
    RETLW DIE6
    RETLW DIE1
    RETLW DIE2
    RETLW DIE3
    RETLW DIE4
    RETLW DIE5
    RETLW DIE6
    RETLW DIE1
    RETLW DIE2
    RETLW DIE3
    RETLW DIE4
    RETLW DIE5
    RETLW DIE6
    RETLW DIE1
    RETLW DIE2
    RETLW DIE3
    RETLW DIE4
    RETLW DIE5
    RETLW DIE6
    RETLW DIE1
    RETLW DIE2
    RETLW DIE3
    RETLW DIE4
    RETLW DIE5
    RETLW DIE6
    RETLW DIE1
    RETLW DIE2
    RETLW DIE3
    RETLW DIE4
    RETLW DIE5
    RETLW DIE6
    RETLW DIE1
    RETLW DIE2
    RETLW DIE3
    RETLW DIE4
    RETLW DIE5
    RETLW DIE6
    RETLW DIE1
    RETLW DIE2
    RETLW DIE3
    RETLW DIE4
    RETLW DIE5
    RETLW DIE6
    RETLW DIE1
    RETLW DIE2
    RETLW DIE3
    RETLW DIE4
    RETLW DIE5
    RETLW DIE6
    RETLW DIE1
    RETLW DIE2
    RETLW DIE3
    RETLW DIE4
    RETLW DIE5
    RETLW DIE6
    RETLW DIE1
    RETLW DIE2
    RETLW DIE3
    RETLW DIE4
    RETLW DIE5
    RETLW DIE6
    RETLW DIE1
    RETLW DIE2
    RETLW DIE3
    RETLW DIE4
    RETLW DIE5
    RETLW DIE6
    RETLW DIE1
    RETLW DIE2
    RETLW DIE3
    RETLW DIE4
    RETLW DIE5
    RETLW DIE6
    RETLW DIE1
    RETLW DIE2
    RETLW DIE3
    RETLW DIE4
    RETLW DIE5
    RETLW DIE6
    RETLW DIE1
    RETLW DIE2
    RETLW DIE3
    RETLW DIE4
    RETLW DIE5
    RETLW DIE6
    RETLW DIE1
    RETLW DIE2
    RETLW DIE3
    RETLW DIE4
    RETLW DIE5
    RETLW DIE6
    RETLW DIE1
    RETLW DIE2
    RETLW DIE3
    RETLW DIE4
    RETLW DIE5
    RETLW DIE6
    RETLW DIE1
    RETLW DIE2
    RETLW DIE3
    RETLW DIE4
    RETLW DIE5
    RETLW DIE6
    RETLW DIE1
    RETLW DIE2
    RETLW DIE3
    RETLW DIE4
    RETLW DIE5
    RETLW DIE6
    RETLW DIE1
    RETLW DIE2
    RETLW DIE3
    RETLW DIE4
    RETLW DIE5
    RETLW DIE6
    RETLW DIE1
    RETLW DIE2
    RETLW DIE3
    RETLW DIE4
    RETLW DIE5
    RETLW DIE6
    RETLW DIE1
    RETLW DIE2
    RETLW DIE3
    RETLW DIE4
    RETLW DIE5
    RETLW DIE6
    RETLW DIE1
    RETLW DIE2
    RETLW DIE3
    RETLW DIE4
    RETLW DIE5
    RETLW DIE6
    RETLW DIE1
    RETLW DIE2
    RETLW DIE3
    RETLW DIE4
    RETLW DIE5
    RETLW DIE6
    RETLW DIE1
    RETLW DIE2
    RETLW DIE3
    RETLW DIE4
    RETLW DIE5
    RETLW DIE6
    RETLW DIE1
    RETLW DIE2
    RETLW DIE3
    

    END
